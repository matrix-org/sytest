use List::Util qw( first );

# No harm in sharing the users as every test runs in its own room
my $creator_fixture = local_user_fixture();

my $user_fixture = local_user_fixture();

sub lockeddown_room_fixture
{
   my ( %options ) = @_;

   fixture(
      requires => [ $creator_fixture, $user_fixture,
                    qw( can_change_power_levels ) ],

      setup => sub {
         my ( $creator, $test_user ) = @_;

         matrix_create_and_join_room( [ $creator, $test_user ], %options )
         ->then( sub {
            my ( $room_id ) = @_;

            matrix_change_room_power_levels( $creator, $room_id, sub {
               my ( $levels ) = @_;

               # Allow users at 80 or above to edit any of the room state
               $_ > 80 and $_ = 80 for values %{ $levels->{events} };
            })->then_done( $room_id );
         });
      },
   );
}

sub test_powerlevel
{
   my ( $name, %args ) = @_;

   my $try = $args{try};
   my @requires = @{ $args{requires} };

   multi_test $name,
      requires => [ $creator_fixture, $user_fixture, lockeddown_room_fixture(),
                    qw( can_change_power_levels ),
                    @requires ],

      do => sub {
         my ( $creator, $test_user, $room_id ) = @_;

         # Fails at powerlevel 0
         matrix_change_room_power_levels( $creator, $room_id, sub {
            my ( $levels ) = @_;
            $levels->{users}{ $test_user->user_id } = 0;
         })->then( sub {
            $try->( $test_user, $room_id )
               ->main::expect_http_403
         })->then( sub {
            pass( "Fails at powerlevel 0" );

            # Succeeds at powerlevel 80
            matrix_change_room_power_levels( $creator, $room_id, sub {
               my ( $levels ) = @_;
               $levels->{users}{ $test_user->user_id } = 80;
            })
         })->then( sub {
            $try->( $test_user, $room_id );
         })->on_done( sub {
            pass( "Succeeds at powerlevel 100" );
         })
      };
}

test_powerlevel "'ban' event respects room powerlevel",
   requires => [qw( can_ban_room )],

   try => sub {
      my ( $test_user, $room_id ) = @_;

      do_request_json_for( $test_user,
         method => "POST",
         uri    => "/r0/rooms/$room_id/ban",

         content => { user_id => '@random_dude:test', reason => "testing" },
      );
   };

# Currently there's no way to limit permission on invites
## test_powerlevel "'invite' event respects room powerlevel",
##    requires => [qw( test_user
##                     can_invite_room )],
## 
##    try => sub {
##       my ( $test_user, $room_id ) = @_;
## 
##       matrix_invite_user_to_room( $test_user, '@random-invitee:$BIND_HOST:8001', $room_id );
##    };

test_powerlevel "setting 'm.room.name' respects room powerlevel",
   requires => [qw( can_set_room_name )],

   try => sub {
      my ( $test_user, $room_id ) = @_;

      matrix_put_room_state( $test_user, $room_id,
         type    => "m.room.name",
         content => { name => "A new room name" },
      );
   };

test_powerlevel "setting 'm.room.power_levels' respects room powerlevel",
   requires => [qw( can_change_power_levels )],

   try => sub {
      my ( $test_user, $room_id ) = @_;

      matrix_change_room_power_levels( $test_user, $room_id, sub {
         my ( $levels ) = @_;
         $levels->{users}{'@some-random-user:here'} = 50;
      });
   };

test "Unprivileged users can set m.room.topic if it only needs level 0",
   requires => [ $creator_fixture, $user_fixture, lockeddown_room_fixture(),
                 qw( can_change_power_levels )],

   do => sub {
      my ( $creator, $test_user, $room_id ) = @_;

      matrix_change_room_power_levels( $creator, $room_id, sub {
         my ( $levels ) = @_;
         delete $levels->{users}{ $test_user->user_id };
         $levels->{events}{"m.room.topic"} = 0;
      })->then( sub {
         matrix_put_room_state( $test_user, $room_id,
            type    => "m.room.topic",
            content => { topic => "Here I can set the topic at powerlevel 0" },
         );
      });
   };

foreach my $levelname (qw( ban kick redact )) {
   multi_test "Users cannot set $levelname powerlevel higher than their own",
      requires => [ $creator_fixture, $user_fixture, lockeddown_room_fixture(),
                    qw( can_change_power_levels )],

      do => sub {
         my ( $user, undef, $room_id ) = @_;

         matrix_change_room_power_levels( $user, $room_id, sub {
            my ( $levels ) = @_;

            $levels->{$levelname} = 25;
         })->SyTest::pass_on_done( "Succeeds at setting 25" )
         ->then( sub {
            matrix_change_room_power_levels( $user, $room_id, sub {
               my ( $levels ) = @_;

               $levels->{$levelname} = 10000000;
            })->main::expect_http_403
         })->SyTest::pass_on_done( "Fails at setting 10000000" );
      };
}

multi_test "Users cannot set notifications powerlevel higher than their own",
   requires => [ $creator_fixture, $user_fixture, lockeddown_room_fixture( room_version => "6" ),
                 qw( can_change_power_levels )],

   do => sub {
      my ( $user, undef, $room_id ) = @_;

      matrix_change_room_power_levels( $user, $room_id, sub {
         my ( $levels ) = @_;

         $levels->{notifications}{room} = 25;
      })->SyTest::pass_on_done( "Succeeds at setting 25" )
      ->then( sub {
         matrix_change_room_power_levels( $user, $room_id, sub {
            my ( $levels ) = @_;

            $levels->{notifications}{room} = 10000000;
         })->main::expect_http_403
      })->SyTest::pass_on_done( "Fails at setting 10000000" );
   };
